just_flags := "--justfile ${DOCKER_RESTIC_CONFIG_DIR}/docker-restic.conf --working-directory ${DOCKER_RESTIC_DATA_DIR}"
restic_flags := "--repo ${DOCKER_RESTIC_DATA_DIR}/repository --password-file /run/secrets/restic-password"
rclone_flags := "--password-command 'cat /run/secrets/rclone-password'"

default:
  @just {{just_flags}} --list

backup:
  @just {{just_flags}} container_stop
  -restic backup /source {{restic_flags}}
  @just {{just_flags}} container_start

backup_prune:
  restic forget {{restic_flags}} \
    --keep-daily ${DOCKER_RESTIC_BACKUP_KEEP_DAILY} \
    --keep-weekly ${DOCKER_RESTIC_BACKUP_KEEP_WEEKLY} \
    --keep-monthly ${DOCKER_RESTIC_BACKUP_KEEP_MONTHLY} \
    --keep-yearly ${DOCKER_RESTIC_BACKUP_KEEP_YEARLY} \
    --group-by paths \
    --prune

backup_check:
  restic check {{restic_flags}} \
    --read-data

backup_check_subset percent:
  restic check {{restic_flags}} \
    --read-data-subset {{percent}}%

sync:
  set -euo pipefail \
    && for remote in $(rclone listremotes {{rclone_flags}} | grep '_default:'); do \
        rclone sync ${DOCKER_RESTIC_DATA_DIR}/repository ${remote} {{rclone_flags}} \
          --stats 15m \
          --fast-list \
          --progress; \
      done

sync_check:
  set -euo pipefail \
    && for remote in $(rclone listremotes {{rclone_flags}} | grep '_default:'); do \
        rclone check ${DOCKER_RESTIC_DATA_DIR}/repository ${remote} {{rclone_flags}} \
          --stats 15m \
          --fast-list \
          --progress; \
      done

dump:
  set -euo pipefail \
    && restic dump latest / {{restic_flags}} \
        --archive tar \
          | gpg \
            --cipher-algo AES256 \
            --passphrase-file /run/secrets/restic-password \
            --output ${DOCKER_RESTIC_DATA_DIR}/export/backup_$(date +'%Y-%m-%d_%H.%M.%S').tar.gpg \
            --compress-level 0 \
            --symmetric \
            --batch \
            --yes \
            --verbose

dump_prune:
  set -euo pipefail \
    && ls -t ${DOCKER_RESTIC_DATA_DIR}/export/backup_* \
      | tail +${DOCKER_RESTIC_DUMP_KEEP_LAST} \
      | xargs -r rm -rf

dump_check:
  set -euo pipefail \
    && ls -t ${DOCKER_RESTIC_DATA_DIR}/export/backup_* \
      | head -1 \
      | xargs -r gpg \
        --passphrase-file /run/secrets/restic-password \
        --decrypt \
        --batch \
        --yes \
        --verbose \
          | tar -tf - > /dev/null

[private]
container_start:
  set -euo pipefail \
  && docker ps \
    --quiet \
    --filter label=docker-restic.container.stop=true \
    --filter status=exited \
      | xargs -r docker restart > /dev/null

[private]
container_stop:
  set -euo pipefail \
  && docker ps \
    --quiet \
    --filter label=docker-restic.container.stop=true \
      | xargs -r docker stop > /dev/null

[private]
container_exec:
  set -euo pipefail \
  && docker ps \
    --quiet \
    --filter label=docker-restic.container.exec \
      | xargs -r -I {} \
        docker inspect --format '{{{{.Id}}}} {{{{index .Config.Labels "docker-restic.container.exec"}}}}' {} \
      | xargs -r -n2 \
        /bin/sh -c 'docker exec $0 /bin/sh -c "$1"'
